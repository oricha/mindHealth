plugins {
    id 'org.springframework.boot' version '3.4.3'
    id 'java'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.github.node-gradle.node' version '7.1.0'
    id 'jacoco'
    id 'checkstyle'
}

group = 'com.mindhealth'
version = '0.0.1-SNAPSHOT'

java.sourceCompatibility = '21'

bootRun {
    // Load .env for local development (DB_*, JWT_*, etc.)
    if (file('.env').exists()) {
        file('.env').readLines().each { line ->
            def trimmed = line.trim()
            if (trimmed && !trimmed.startsWith('#') && trimmed.contains('=')) {
                def sep = trimmed.indexOf('=')
                def key = trimmed.substring(0, sep).trim()
                def value = trimmed.substring(sep + 1).trim().replaceAll(/^["']|["']$/, '')
                if (key && !key.startsWith('#')) {
                    environment key: value
                }
            }
        }
    }
    // Default to dev profile for local bootRun (connects to localhost Postgres/Redis)
    environment SPRING_PROFILES_ACTIVE: environment.SPRING_PROFILES_ACTIVE ?: 'dev'
}

springBoot {
    mainClass = 'com.mindhealth.mindhealth.MindhealthApplication'
}

bootJar {
    mainClass = 'com.mindhealth.mindhealth.MindhealthApplication'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:3.4.3'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf:3.4.3'
    implementation 'org.springframework.boot:spring-boot-starter-validation:3.4.3'
    implementation 'org.springframework.boot:spring-boot-starter-web:3.4.3'
    implementation 'org.springframework.boot:spring-boot-starter-actuator:3.4.3'
    implementation 'org.springframework.boot:spring-boot-starter-security:3.4.3'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client:3.4.3'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis:3.4.3'
    implementation 'org.springframework.boot:spring-boot-starter-mail:3.4.3'
    implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:3.3.0'
    implementation 'org.postgresql:postgresql:42.7.4'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'
    implementation 'com.amazonaws:aws-java-sdk-s3:1.12.773'
    implementation 'org.projectlombok:lombok:1.18.36'
    implementation 'com.stripe:stripe-java:24.0.0'
    implementation 'org.apache.pdfbox:pdfbox:2.0.32'
    implementation 'com.google.zxing:core:3.5.3'
    implementation 'com.google.zxing:javase:3.5.3'
    implementation 'io.micrometer:micrometer-core:1.13.6'
    implementation 'org.flywaydb:flyway-core:10.20.0'
    implementation 'org.flywaydb:flyway-database-postgresql:10.20.0'
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'
    developmentOnly 'org.springframework.boot:spring-boot-devtools:3.4.3'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:3.4.3'
    testImplementation 'org.testcontainers:postgresql:1.20.0'
    testImplementation 'org.testcontainers:junit-jupiter:1.20.2'
}

node {
    download.set(true)
    version.set('22.14.0')
}

tasks.register('npmRunBuild', NpmTask) {
    args = ['run', 'build']
    dependsOn npmInstall

    inputs.files(fileTree('node_modules'))
    inputs.files(fileTree('src/main/resources'))
    inputs.file('package.json')
    inputs.file('tsconfig.json')
    inputs.file('webpack.config.js')
    outputs.dir(layout.buildDirectory.dir('resources/main/static'))
}

processResources {
    dependsOn npmRunBuild
}

tasks.withType(Test) {
    useJUnitPlatform()
}

tasks.named('resolveMainClassName').configure {
    enabled = false
}
